buildscript {
    ext.kotlin_version = '1.2.71'
    ext.spring_boot_version = '2.0.0.BUILD-SNAPSHOT'
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://repo.spring.io/libs-snapshot' }
        maven { url 'https://repo.spring.io/libs-milestone' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version '1.2.71'
    id 'org.springframework.boot' version '2.0.5.RELEASE' // does not support snapshots
    id 'io.spring.dependency-management' version '1.0.1.RELEASE'
    id 'com.gorylenko.gradle-git-properties' version '1.4.17'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'idea'

group = 'com.michalkowol'
version = '0.1.0'

defaultTasks 'bootRun'

springBoot  {
    buildInfo()
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

jar {
    baseName = "${project.name}-assembly"
    version = project.version
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

idea {
   module {
      inheritOutputDirs = false
      outputDir = file("$buildDir/classes/kotlin/main")
   }
}

sourceSets {
    integrationTest {
        kotlin {
            srcDirs += 'src/it/kotlin'
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources.srcDirs += 'src/it/resources'
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

integrationTest {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

task stage(dependsOn: ['assemble', 'clean'])
assemble.mustRunAfter clean
stage.dependsOn(assemble)

jacocoTestReport {
    reports {
        csv.enabled false
        html.enabled true
        xml.enabled true
    }
    dependsOn test
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    maven { url 'http://dl.bintray.com/kotlin/kotlinx.coroutines' }
    maven { url 'https://repo.spring.io/libs-snapshot' }
    maven { url 'https://repo.spring.io/libs-milestone' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    ext.jackson_version = '2.9.0.pr2'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    
    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile 'org.springframework.boot:spring-boot-starter-jetty'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-actuator'

    compile 'org.slf4j:slf4j-api:1.7.+'
    compile 'ch.qos.logback:logback-classic:1.1.+'

    compile 'javax.inject:javax.inject:1'

    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'javax.el:el-api:2.+'
    compile 'com.zaxxer:HikariCP:2.5.+'
    compile 'org.postgresql:postgresql:9.4.+'
    compile 'com.h2database:h2:1.4.+'
    compile 'org.flywaydb:flyway-core:4.1.+'

    compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jackson_version"
    compile 'org.codehaus.woodstox:woodstox-core-asl:4.4.+'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'com.natpryce:hamkrest:1.3.+'
    testCompile 'com.nhaarman:mockito-kotlin:1.4.+'

    integrationTestCompile 'com.ninja-squad:DbSetup-kotlin:2.1.+'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}
